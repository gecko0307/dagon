# Tutorial 15. Terrain

Terrain is a mesh that imitates the surface of a land and is generated by the engine from height (elevation) data. In Dagon, you can create a terrain from image-based height map, or fully procedurally using a gradient noise generator such as OpenSimplex.

## Generating a terrain

A height map is just a grayscale image where darker pixels represent lower elevations and lighter pixels represent higher elevations. Mesh vertices are generated via bilinear interpolation of the image using the `ImageHeightmap` proxy object. In Dagon, black is interpreted as zero elevation (relative to the terrain entity's origin), and white corresponds to the maximum elevation in local space, controlled by the `ImageHeightmap.scale` parameter.

The resolution of the height map image does not need to match the vertex resolution of the terrain. The `Terrain` class is created using two resolution parameters: the render mesh resolution and the collision mesh resolution. The collision mesh is used for physics interactions and ray casting â€” such as character movement, terrain collision, and picking.

The size of the terrain in local space equals the resolution of the render mesh (i.e., the spacing between vertices is 1 unit). The collision mesh is automatically scaled to match the visual size of the terrain, ensuring consistent alignment.

```d
class TerrainScene: Scene
{
    TextureAsset aHeightmap;
    
    override void beforeLoad()
    {
        aHeightmap = addTextureAsset("assets/heightmap.png");
        aHeightmap.persistent = true; // required, we'll need CPU-side access to the image data
    }
    
    override void afterLoad()
    {
        // Creates a height map with maximum elevation 30 meters
        auto heightmap = New!ImageHeightmap(aHeightmap.image, 30.0f, assetManager);
        
        // Render mesh resolution = 512x512 vertices
        // Collision mesh resolution = 64x64 vertices
        auto terrain = New!Terrain(512, 64, heightmap, assetManager);
        auto eTerrain = addEntity();
        eTerrain.dynamic = false;
        eTerrain.solid = true;
        eTerrain.drawable = terrain;
    }
}
```

## Creating a terrain material

Texturing a terrain is a special case in Dagon's deferred pipeline. It is done with `environment.terrainMaterial`. This special multi-texturing material supports multiple layers, each of which is itself a normal `Material` with all standard textures and settings plus a `maskTexture` which assigns an alpha mask (which is sometimes called a splat map) to the layer, where black means the layer is invisible and white means fully visible. First layer usually doesn't have an alpha mask.

```d
auto terrainMaterial = environment.terrainMaterial;

auto layer1 = terrainMaterial.addLayer();
layer1.baseColorTexture = aTexSandAlbedo.texture;
layer1.normalTexture = aTexSandNormal.texture;
layer1.roughnessFactor = 0.2f;
layer1.textureScale = Vector2f(50, 50);

auto layer2 = terrainMaterial.addLayer();
layer2.baseColorTexture = aTexGrassAlbedo.texture;
layer2.normalTexture = aTexGrassNormal.texture;
layer2.roughnessFactor = 0.9f;
layer2.maskTexture = aSplatmapGrass.texture;
layer2.textureScale = Vector2f(50, 50);

eTerrain.material = terrainMaterial;
```

All terrain textures must be tileable (repeatable) to avoid visible seams. Terrain layers support all standard PBR maps, including normal, roughness+metallic and emission.

The system has no limit on the number of layers, but each additional layer adds a separate fullscreen rendering pass, which may impact performance, so it is adviced to keep the number of layers small. For a greater variety of colors, you can use a "detail map" technique: a color texture as the first layer, and one or more tiled normal maps as the second, third, and so on. The key is to disable color output for detail layers with the `outputColor` parameter, so that the color of the terrain will be always defined by the first layer.

```d
auto layer1 = terrainMaterial.addLayer();
layer1.baseColorTexture = aTerrainAlbedo.texture;
layer1.roughnessFactor = 0.2f;

auto layer2 = terrainMaterial.addLayer();
layer2.normalTexture = aTerrainNormal1.texture;
layer2.textureScale = Vector2f(50, 50);
layer2.outputColor = false;

auto layer3 = terrainMaterial.addLayer();
layer3.normalTexture = aTerrainNormal2.texture;
layer3.textureScale = Vector2f(50, 50);
layer3.maskTexture = aTerrainSplatmap2.texture;
layer3.outputColor = false;
```

Similarly, `outputNormal`, `outputPBR`, and `outputEmission` can be disabled as needed.

**Note:** the `TerrainMaterial` is a scene-level singleton and is shared across all terrain instances due to the nature of deferred texturnig. All terrains in the scene are composited into a common deferred buffer, so mixing multiple terrain materials is not supported by design.
